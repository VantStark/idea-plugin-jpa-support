buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'io.freefair.gradle:lombok-plugin:5.3.0'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'io.freefair.lombok' version '5.3.0'
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'io.freefair.lombok'

group 'com.ifengxue'

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.2.Final'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.4.0'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.18'
    compileOnly fileTree('lib')
    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    pluginName 'Jpa Support'
    type 'IC'
    def localPropertyFile = project.rootProject.file('local.properties')
    Properties properties = new Properties()
    if (localPropertyFile.exists()) {
        properties.load(localPropertyFile.newDataInputStream())
    }
    def localIdePath = properties.get('local_ide_path');
    if (localIdePath != null && !localIdePath.isEmpty()) {
        localPath = localIdePath
        printf('use local path: %s', localIdePath)
    } else {
        version '2020.3.2'
        printf('use remote version: %s', version)
    }
    updateSinceUntilBuild false
    plugins = ['java']
}

runIde {
    systemProperty('idea.auto.reload.plugins', true)
}

patchPluginXml {
    version '2.0.7-RC2'
    changeNotes """
      <ol>
          <li>Allow add Swagger UI annotation</li>
          <li>Allow add schema/catalog to Entity</li>
          <li>Enhance the Database Plugin, support loading unselected tables</li>
          <li>Enhanced the ignore field feature, allowing fields to be dynamically ignored when editing the table structure</li>
          <li>fix some problems</li>
      </ol>
      """
}
